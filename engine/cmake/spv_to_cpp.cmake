if(DEFINED EMBED_RESOURCE_ENTRY)
    set(resource_file_name "${SPV_FILE}")
    set(source_file_name "${CPP_FILE}")
    set(variable_name "${GLOBAL_VAR}")
    if(EXISTS "${source_file_name}")
        if("${source_file_name}" IS_NEWER_THAN "${resource_file_name}")
            return()
        endif()
    endif()
    if(EXISTS "${resource_file_name}")
        file(READ "${resource_file_name}" hex_content HEX)
        string(REPEAT "[0-9a-f]" 32 pattern)
        string(REGEX REPLACE "(${pattern})" "\\1\n" content "${hex_content}")
        string(REGEX REPLACE "([0-9a-f][0-9a-f])" "0x\\1, " content "${content}")
        string(REGEX REPLACE ", $" "" content "${content}")
        set(array_definition "static const std::vector<unsigned char> ${variable_name} =\n{\n${content}\n};")
        get_filename_component(file_name ${source_file_name} NAME)
        set(source "/**\n * @file ${file_name}\n * @brief Auto generated file.\n */\n#include <vector>\n${array_definition}\n")
        file(WRITE "${source_file_name}" "${source}")
    else()
        message(FATAL_ERROR "Resource file doesn't exist: ${resource_file_name}")
    endif()
    return()
endif()

